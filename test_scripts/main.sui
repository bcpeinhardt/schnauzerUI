# This sui script contains most of the tests for all SchnauzerUI functionality.
# Navigate to the dedicated testing site.
url "https://bcpeinhardt.github.io/SchnauzerUITestingSite/"

# Test locating label and typing into associated text input using for attribute
locate "First Name" and type "Ben Ben"

# Test locating label and typing into contained text input
locate "Last Name" and type "Peinhardt"

# Test locating button by its text and click it. Screenshot sould show the button
# has a green background color.
locate "Submit" and click

# Test variable by saving text and typing into field
save "Wow" as wow
locate "Type the text Wow into this field, but save the text as a variable first" and type wow

# Test type into input if it is present using if stmt
if locate "Type into me if I exist" then type "located with if stmt"

# Test locate doesn't cause error in if stmt 
if locate "I dont exist" then click

# Test catch-error recovers from error and reports correctly
# This stmt SHOULD SHOW AN ERROR but the script should continue
locate "I dont exist"
catch-error: screenshot

# Locator tests
locate "partial text test" and click
locate "locate-by-title" and click
locate "locate-by-id" and click
locate "locate-by-class" and click
locate "locate-by-name" and click
locate "//p[@id='locate-by-xpath']" and click

# Do a locate-no-scroll to make sure it doesn't error
# Don't have a good way of testing much beyond that
locate-no-scroll "partial text test"

# Test writing the text of an element to an input
locate "read-to-test" and read-to test_txt
locate "Write the text here..." and type test_txt

# Locator tests with Under
under "Under Command" locate "partial text test" and click
under "Under Command" locate "locate-by-title" and click
under "Under Command" locate "locate-by-id" and click
under "Under Command" locate "locate-by-class" and click
under "Under Command" locate "locate-by-name" and click
under "Under Command" locate "//p[@id='locate-by-xpath']" and click

# Take final test screenshot for visual inspection
screenshot